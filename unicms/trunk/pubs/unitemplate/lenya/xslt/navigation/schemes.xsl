<?xml version="1.0" encoding="UTF-8" ?>


<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xhtml="http://www.w3.org/1999/xhtml"
    xmlns:unizh="http://unizh.ch/doctypes/elements/1.0"
    xmlns:uz="http://unizh.ch"
    xmlns:dc="http://purl.org/dc/elements/1.1/"
    xmlns:lenya="http://apache.org/cocoon/lenya/page-envelope/1.0"
    >

<!-- This file patches the navigational components generated by lenya until lenya supports

- multiple navigation schemes
- doctype specific navigation schemes

-->

<xsl:variable name="index" select="/document/xhtml:div[@id = 'menu']/xhtml:div[@basic-url = 'index' and @current = 'true']"/>
<xsl:variable name="homepage" select="/document/content/unizh:homepage"/>
<xsl:variable name="publication-tabs" select="/document/uz:unizh/uz:publication/@tabs"/>
<xsl:variable name="subhome-tabs">
  <xsl:choose>
    <xsl:when test="$homepage">
      <xsl:value-of select="/document/content/unizh:homepage/@unizh:tabs"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor/unizh:homepage/@unizh:tabs"/>
    </xsl:otherwise>
  </xsl:choose>
</xsl:variable>

<xsl:template match="xhtml:div[@id = 'menu']">
  <xhtml:div id="menu">
    <xsl:choose>
      <xsl:when test="$publication-tabs = 'true'"> <!-- tabs enabled -->
        <xsl:choose>
          <xsl:when test="$homepage"> 
            <xsl:if test="not($index) and ($subhome-tabs = 'root' or $subhome-tabs = 'false')">
              <xsl:if test="$subhome-tabs = 'root'"> 
                <xsl:attribute name="href">
                  <xsl:value-of select="descendant::xhtml:div[@current = 'true']/@href"/>
                </xsl:attribute>
                <xsl:attribute name="label">
                  <xsl:value-of select="descendant::xhtml:div[@current = 'true']/node()"/>
                </xsl:attribute>
              </xsl:if> 
              <xsl:apply-templates select="descendant::xhtml:div[@current = 'true']/xhtml:div"/>
            </xsl:if>
          </xsl:when>
          <xsl:otherwise>
            <xsl:choose>
             <xsl:when test="/document/unizh:ancestors/unizh:ancestor[unizh:homepage][1]/@basic-url = 'index'">
               <xsl:apply-templates select="xhtml:div[descendant-or-self::xhtml:div[@current = 'true']]/xhtml:div"/>
             </xsl:when>
             <xsl:otherwise>
               <xsl:choose>
                 <xsl:when test="$subhome-tabs = 'true'">
                    <xsl:apply-templates select="descendant::xhtml:div[@basic-url = /document/unizh:ancestors/unizh:ancestor[unizh:homepage]/@basic-url]/xhtml:div[descendant-or-self::xhtml:div[@current = 'true']]/xhtml:div"/>  
                 </xsl:when>
                 <xsl:otherwise>
                   <xsl:if test="$subhome-tabs = 'root'">  
                     <xsl:attribute name="href">
                       <xsl:value-of select="descendant::xhtml:div[@current = 'true']/@href"/>
                     </xsl:attribute> 
                     <xsl:attribute name="label">
                       <xsl:value-of select="descendant::xhtml:div[@current = 'true']/node()"/>
                     </xsl:attribute>
                   </xsl:if>
                   <xsl:apply-templates select="descendant::xhtml:div[@basic-url = /document/unizh:ancestors/unizh:ancestor[unizh:homepage]/@basic-url]/xhtml:div"/> 
                 </xsl:otherwise>
               </xsl:choose>
             </xsl:otherwise>
            </xsl:choose>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:when>
      <xsl:otherwise> <!-- tabs disabled -->
        <xsl:choose> 
          <xsl:when test="$homepage"> 
            <xsl:choose>
               <xsl:when test="$index">
                 <xsl:apply-templates select="xhtml:div[not(@basic-url = 'index')]"/>
               </xsl:when>
               <xsl:otherwise>
                 <xsl:apply-templates select="descendant::xhtml:div[@current = 'true']/xhtml:div"/>
               </xsl:otherwise>
            </xsl:choose>
          </xsl:when>
          <xsl:otherwise> 
            <xsl:choose>
              <xsl:when test="/document/unizh:ancestors/unizh:ancestor[unizh:homepage][1]/@basic-url = 'index'">
                <xsl:apply-templates select="xhtml:div[not(@basic-url = 'index')]"/> 
              </xsl:when>
              <xsl:otherwise>
                <xsl:apply-templates select="descendant::xhtml:div[@basic-url = /document/unizh:ancestors/unizh:ancestor[unizh:homepage]/@basic-url]/xhtml:div"/> 
              </xsl:otherwise>
            </xsl:choose>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:otherwise>
    </xsl:choose>
  </xhtml:div>
</xsl:template>


<!-- tabs -->

<xsl:template match="xhtml:div[@id = 'tabs']">
  <xsl:if test="$publication-tabs = 'true'">
    <xhtml:div id="tabs">
      <xsl:choose>
        <xsl:when test="$homepage">
          <xsl:if test="$index or $subhome-tabs = 'root'">
            <xsl:apply-templates/>
          </xsl:if>
          <xsl:if test="not($index) and $subhome-tabs = 'true'">
            <xsl:apply-templates select="//xhtml:div[ancestor::xhtml:div[@id = 'menu'] and @current = 'true']/xhtml:div"/> 
          </xsl:if>
        </xsl:when>
        <xsl:otherwise>
          <xsl:if test="/document/unizh:ancestors/unizh:ancestor[unizh:homepage][1]/@basic-url = 'index' or $subhome-tabs = 'root'">
            <xsl:apply-templates/>
          </xsl:if>
          <xsl:if test="$subhome-tabs = 'true'">
            <xsl:apply-templates select="//xhtml:div[ancestor::xhtml:div[@id = 'menu'] and @basic-url=/document/unizh:ancestors/unizh:ancestor[unizh:homepage]/@basic-url]/xhtml:div"/>         
          </xsl:if>
        </xsl:otherwise>
      </xsl:choose>
    </xhtml:div>
  </xsl:if>
</xsl:template> 


<!-- header -->
 
<xsl:template match="unizh:header">
  <unizh:header>
    <xsl:choose>
      <xsl:when test="$homepage">
        <xsl:choose> 
          <xsl:when test="not($index) and $subhome-tabs = 'root'">
            <unizh:superscription>
               <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor[@basic-url = 'index']/unizh:homepage/unizh:header/unizh:superscription"/>
            </unizh:superscription>
            <unizh:heading href="{/document/unizh:ancestors/unizh:ancestor[@basic-url = 'index']/@href }">
              <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor[@basic-url = 'index']/unizh:homepage/unizh:header/unizh:heading"/>
            </unizh:heading>
          </xsl:when>
          <xsl:otherwise>
            <unizh:superscription>
              <xsl:value-of select="/document/content/unizh:homepage/unizh:header/unizh:superscription"/>
            </unizh:superscription>
            <unizh:heading>
              <xsl:value-of select="/document/content/unizh:homepage/unizh:header/unizh:heading"/>
            </unizh:heading>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:when>
      <xsl:otherwise>
        <xsl:choose>
          <xsl:when test="/document/unizh:ancestors/unizh:ancestor[unizh:homepage][1]/@basic-url = 'index'">
            <unizh:superscription>
              <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor[@basic-url = 'index']/unizh:homepage/unizh:header/unizh:superscription"/>
            </unizh:superscription>
            <unizh:heading href="{/document/unizh:ancestors/unizh:ancestor[@basic-url = 'index']/@href}">
              <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor[@basic-url = 'index']/unizh:homepage/unizh:header/unizh:heading"/>              
            </unizh:heading>
          </xsl:when>
          <xsl:otherwise>
            <unizh:superscription>
              <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor/unizh:homepage/unizh:header/unizh:superscription"/>
            </unizh:superscription> 
            <unizh:heading href="{/document/unizh:ancestors/unizh:ancestor[unizh:homepage]/@href}">
              <xsl:value-of select="/document/unizh:ancestors/unizh:ancestor/unizh:homepage/unizh:header/unizh:heading"/>
            </unizh:heading> 
          </xsl:otherwise>
        </xsl:choose> 
      </xsl:otherwise>
    </xsl:choose>
  </unizh:header>
</xsl:template>





<!-- breadcrumb path -->

<xsl:template match="xhtml:div[@id = 'breadcrumb']">
  <xhtml:div id="breadcrumb">
    <xsl:for-each select="xhtml:div">
      <xsl:choose>
        <xsl:when test="following-sibling::xhtml:div[@basic-url = /document/unizh:ancestors/unizh:ancestor[unizh:homepage]/@basic-url]">
        </xsl:when>
        <xsl:when test="$homepage">
          <xsl:apply-templates select="self::xhtml:div[@current = 'true']"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:apply-templates select="."/>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:for-each>
  </xhtml:div>
</xsl:template> 


<!-- Doctype News. Hide children nodes in menu -->

<xsl:template match="xhtml:div[/document/content/unizh:news and ancestor::xhtml:div[@id = 'menu'] and parent::xhtml:div[@current = 'true']]"/>

<!-- Doctype Newsitem. Hide current level in menu -->

<xsl:template match="xhtml:div[/document/content/unizh:newsitem and ancestor::xhtml:div[@id = 'menu'] and (@current = 'true' or ../xhtml:div/@current = 'true')]"/>


<xsl:template match="@*|node()">
  <xsl:copy>
    <xsl:apply-templates select="@*|node()"/>
  </xsl:copy>
</xsl:template>

</xsl:stylesheet> 
